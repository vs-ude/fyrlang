type List<T> struct {
    first mut *Element<T>
    last mut *Element<T>
    count int
}

type Element<T> struct {
    Value T
    prev mut *Element
    next mut *Element
}

func dual *List.First() -this dual *Element<T> {
    return this.first
}

func dual *List.Last() -this dual *Element<T> {
    return this.last
}

func *List.Size() int {
    return this.count
}

func mut *List.Clear() {
    // TODO: Store the old elements for later use?
    this.last = null
    this.first = null
    this.count = 0
}

func *List.IsEmpty() bool {
    return this.count == 0
}

func mut *List.Push(value -this T) {
    let e mut *Element<T> = {Value: value, prev: this.last}
    if this.last != null {
        this.last.next = e
    } else {
        this.first = e
    }
    this.last = e
    this.count++
}

func mut *List.Pop() -this T {
    last := this.last
    if this.last == this.first {
        this.first = null
        this.last = null
        this.count = 0
    } else {
        this.last = this.last.prev
        this.last.next = null
        this.count--
    }
    return take(last.Value)
}

// func List.Shift(value T) {
// }

// func List.Unshift() T {
// }

func dual *List.At(pos int) -this dual *Element<T> {
    e := this.first
    for i := 0; i < pos && e != null; i++ {
        e = e.next
    }
    return e
}

// Erases the element from the list and returns the next element in the list.
func mut *List.Erase(it -this mut *Element<T>) -this mut *Element<T> {
    // TODO: Reclaim the removed element
    next := take(it.next)
    prev := take(it.prev)
    if prev == null {
        if it != this.first {
            println("Iterator on wrong list")
        }
        if next == null {
            this.last = null
            this.first = null
        } else {
            this.first = next
            this.first.prev = null
        }
        this.count--
        return this.first
    } else if next == null {
        if it != this.last {
            println("Iterator on wrong list")
        }
        this.last = prev
        this.last.next = null
        this.count--
        return null
    }
    prev.next = next
    next.prev = prev
    this.count--
    return next
}

func mut *List.Concat(l -this mut *List) {
    if this.first == null {
        this.first = take(l.first)
        this.last = take(l.last)
        this.count = take(l.count)
        return
    }
    if l.first == null {
        return
    }
    e := take(l.first)
    e.prev = this.last
    this.last.next = e
    this.last = take(l.last)
	// TODO
    // this.count += l.count
	this.count = this.count + take(l.count)
}

func dual *Element.Next() -this dual *Element {
    return this.next
}

func dual *Element.Prev() -this dual *Element {
    return this.prev
}
